seed(12345)
mvi = 0
mni = 0

# Read in the character data, note there is some missing character data notated by '?'. 
data = readCharacterData("romanceAlignments.nex")

nchar = data.nchar()

## for (c in 1:nchar) {
##   cdata[c] = readCharacterData("romanceAlignments.nex")
##   cdata[c].excludeAll()
##   cdata[c].includeCharacter(c)
## }

# To accommodate for phylogenetic uncertainty, we will run our analyses over a
# posterior distribution of trees estimated in a previous analysis. We do this
# by sampling from an empirical tree distribution constructed from the tree trace
# output of the previous MCMC analysis.

# read in tree trace
treetrace = treeTrace(readTrees("romance.posterior.tree",
                                treetype="non-clock"), burnin=0)

# draw a tree from the empirical tree distribution
psi ~ dnEmpiricalTree(treetrace)
moves[++mvi] = mvEmpiricalTree(psi, metropolisHastings=FALSE)



# rate of change

rateMultiplier ~ dnExp(1)
moves[++mvi] = mvScale(rateMultiplier, lambda=1.0)


alpha_prior_mean <- ln(5.0)
alpha_prior_sd <- 0.587405
alpha ~ dnLognormal(alpha_prior_mean, alpha_prior_sd)
sr := fnDiscretizeGamma(alpha, alpha, 4)
moves[++mvi] = mvScale(alpha, lambda=1.0)




k = 37

for (i in 1:3) {
    rates[i] ~ dnLoguniform(0.001, 10)
    moves[++mvi] = mvScale(rates[i], lambda=1)
}

for (i in 1:k) {
    for (j in 1:k) {
        if (i == j) q[i][j] := 0.0
        if (i != j & i == k) q[i][j] := rates[1]
        if (i != j & j == k) q[i][j] := rates[2]
        if (i != j & i < k & j < k) q[i][j] := rates[3]
    }
}
Q := fnFreeK(q)
                
nrates[1] := Q[1][k]*rateMultiplier
nrates[2] := Q[k][1]*rateMultiplier
nrates[3] := Q[1][2]*rateMultiplier


seq ~ dnPhyloCTMC(Q=Q,
                  tree=psi,
                  rootFrequencies=simplex(rep(1, k)),
                  siteRates=sr,
                  branchRates=rateMultiplier,
                  type="Standard")
seq.clamp(data)


siteRates := seq.siteRates()


mymodel = model(seq)

monitors[++mni] = mnScreen(printgen=10)

monitors[++mni] = mnFile(filename="output/soundRates.log",
                         printgen=10)
monitors[mni].addVariable(alpha)
monitors[mni].addVariable(nrates)
monitors[mni].addVariable(siteRates)


###############
# run MCMC analysis
###############


mymcmc = mcmcmc(mymodel, monitors, moves, nruns=4, nchains=4)
mymcmc.burnin(500, tuningInterval=100)
mymcmc.run(2000)

mymcmc.operatorSummary()

q()
